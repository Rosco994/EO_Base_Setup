// Copyright (c) Vult-r
//
// Use of this source code is governed by an MIT-style
// license that can be found in the LICENSE file or at
// https://opensource.org/licenses/MIT.

Main
{
	questname 	"Rat Problems"
	version		1.0
}

state Begin
{
	desc		"Kill the rat, inside old farm"
	action		ShowHint("Quest reward : 1710 EXP")
	action		AddNpcText( 35 , "There must be a rat inside my house, there holes in my bags of grain everywhere.." );
	action		AddNpcText( 35 , "Can you find and kill this critter for me?" );

	action		AddNpcInput( 35 , 1 , "No thanks");
	action		AddNpcInput( 35 , 2 , "I'm working on it!");

	rule 		KilledNpcs( 2, 1) goto Talk2
	rule 		InputNpc( 1 ) goto Reset

}

state Talk2
{
	desc		"Talk to the old farmer"
	action		ShowHint("You successfully killed the rat!")
	action		AddNpcChat( 35 , "Thank you!" );
	action		AddNpcText( 35 , "Thank you for removing that rat!" );
	action		AddNpcText( 35 , "Actually my basement is crowded with fierce rats and my wife doesn't want to live here anymore." );
	action		AddNpcText( 35 , "Please kill all rats in my basement and I will reward you for your efforts." );
	action		AddNpcText( 35 , "You can enter the basement on the upper right corner of my house, I just unlocked the door for you." );

	action		AddNpcInput( 35 , 1 , "No thanks");
	action		AddNpcInput( 35 , 2 , "I'm working on it!");

	rule 		InputNpc( 1 ) goto Reset
	rule		EnterCoord( 3 , 0 , 3) goto KillBasement
}

state KillBasement
{
	desc		"Kill 20 fierce rat."
	action		SetCoord( 284, 3, 2)

	rule 		KilledNpcs( 270, 20) goto Talk3
	rule		EnterMap( 3 ) goto Talk4

}

state Talk4
{
	desc		"Talk to the old farmer"
	action		ShowHint("Talk to the old farmer")
	action		AddNpcChat( 35 , "Oh no.." );
	action		AddNpcText( 35 , "My basement is still full of rats, my wife is going to leave me now, try harder next time." );

	rule 		TalkedToNpc( 35 ) goto Reset

}

state Talk3
{
	desc		"Talk to the old farmer"
	action		ShowHint("You successfully killed 20 fierce rats!")
	action		AddNpcChat( 35 , "Thank you so much for killing those rats!" );
	action		AddNpcText( 35 , "Thank you so much for killing those rats! My wife and I are very happy now! Here is your reward" );

	rule 		TalkedToNpc( 35 ) goto GetReward

}

state GetReward
{
	action		ShowHint("You gained 1710 EXP!");
	action		PlaySound(17);

	action		GiveExp(1710)
	action		End();
}

state Reset
{
	action		ShowHint("Rat problems quest aborted..")
	action		Reset()
}	
